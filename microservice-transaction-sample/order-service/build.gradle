plugins {
    id 'java'
    id 'java-library-distribution'
    id 'application'
}

dependencies {
    implementation project(':rpc')
    implementation "com.scalar-labs:scalardb:${scalarDbVersion}"
    implementation "info.picocli:picocli:${picoCliVersion}"
    implementation "org.apache.logging.log4j:log4j-api:${log4jVersion}"
    implementation "org.apache.logging.log4j:log4j-core:${log4jVersion}"
    implementation "org.apache.logging.log4j:log4j-slf4j-impl:${log4jVersion}"
}

application {
    mainClass = 'sample.order.OrderServiceServer'
}

task copyFilesToDockerBuildContextDir(type: Copy) {
    description 'Copy files to a temporary folder to build the Docker image'
    dependsOn distTar
    from('src/main/resources/log4j2.properties')
    from('order-service.properties')
    from('Dockerfile')
    from(tasks.distTar.archiveFile)
    into('build/docker')
}

task docker(type: Exec) {
    description 'Build Order Service Docker image'
    dependsOn copyFilesToDockerBuildContextDir
    workingDir 'build/docker'
    commandLine 'docker', 'build', "--tag=sample-order-service:${project.version}", "."
}

distTar {
    archiveFileName = "${project.name}.tar"
    duplicatesStrategy DuplicatesStrategy.EXCLUDE
}

distZip {
    duplicatesStrategy DuplicatesStrategy.EXCLUDE
}

installDist {
    duplicatesStrategy DuplicatesStrategy.EXCLUDE
}

base {
    archivesName = "sample-order-service"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}
