plugins {
    id 'java'
    id 'java-library-distribution'
    id 'com.google.protobuf' version '0.9.4'
}

dependencies {
    api "io.grpc:grpc-netty:${grpcVersion}"
    api "io.grpc:grpc-protobuf:${grpcVersion}"
    api "io.grpc:grpc-stub:${grpcVersion}"
    api "io.grpc:grpc-services:${grpcVersion}"
    implementation "javax.annotation:javax.annotation-api:1.3.2"
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

base {
    archivesName = "sample-rpc"
}

// Task copyGeneratedProtoToSrc copies the generated .java files into src directory
task copyGeneratedProtoToSrc(type: Copy) {
    description 'Copies generated Protocol Buffer classes to src/main/java/sample/rpc'
    dependsOn generateProto
    from "$buildDir/generated/source/proto/main/java/sample/rpc"
    into "$projectDir/src/main/java/sample/rpc"
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

// Task deleteGeneratedProto deletes the generated .java files in build directory
task deleteGeneratedProto(type: Delete) {
    dependsOn copyGeneratedProtoToSrc
    delete fileTree(dir: "$buildDir/generated/source/proto/main/java/sample/rpc")
}

// The processResources task needs to depend on the generateProto task because it uses the output
// of the the generateProto task
processResources {
    dependsOn generateProto
}

// Task deleteGeneratedProto needs to depend on deleteGeneratedProto to avoid duplicate class error
tasks.named("compileJava").configure {
    dependsOn deleteGeneratedProto
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}
