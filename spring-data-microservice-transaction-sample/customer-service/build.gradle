plugins {
    id 'java'
    id 'java-library-distribution'
    id 'application'
}

dependencies {
    implementation project(':rpc')

    implementation "org.slf4j:slf4j-simple:${slf4jVersion}"
    implementation "com.scalar-labs:scalardb-sql-spring-data:${scalarDbVersion}"
    implementation "com.scalar-labs:scalardb-cluster-java-client-sdk:${scalarDbVersion}"
    implementation "info.picocli:picocli-spring-boot-starter:${picoCliVersion}"
    implementation "org.springframework.boot:spring-boot-starter-aop:${springBootVersion}"
    implementation "org.springframework.retry:spring-retry:${springRetryVersion}"
}

application {
    mainClass = 'sample.customer.CustomerServiceServer'
}

task copyFilesToDockerBuildContextDir(type: Copy) {
    description 'Copy files to a temporary folder to build the Docker image'
    dependsOn distTar
    from('src/main/resources/application.properties')
    from('customer-service.properties')
    from('Dockerfile')
    from(tasks.distTar.archiveFile)
    into('build/docker')
}

task docker(type: Exec) {
    description 'Build Customer Service Docker image'
    dependsOn copyFilesToDockerBuildContextDir
    workingDir 'build/docker'
    commandLine 'docker', 'build', "--tag=sample-customer-service:${project.version}", "."
}

distTar {
    archiveFileName = "${project.name}.tar"
    duplicatesStrategy DuplicatesStrategy.EXCLUDE
}

distZip {
    duplicatesStrategy DuplicatesStrategy.EXCLUDE
}

installDist {
    duplicatesStrategy DuplicatesStrategy.EXCLUDE
}

base {
    archivesName = "sample-customer-service"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}
